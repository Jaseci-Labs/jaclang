import:py from jaclang.core.llms, OpenAI;
import:py from PIL, Image;

glob llm = OpenAI(model_name="gpt-4o");

obj 'Purchased Item'
PurchasedItem {
    has name: 'Item Name': str,
        price: 'Item Price': float,
        quantity: 'Item Quantity': int;
}

obj 'Receipt'
Receipt {
    has store: 'Store Name': str,
        date: 'Purchase Date': str,
        items: 'Items': list[PurchasedItem],
        total: 'Total': float;

    can pp() -> None {
        print(f"Store: {self.store}");
        print(f"Date: {self.date}");
        for item in self.items {
            print(f"{item.name} - {item.price} x {item.quantity}");
        }
        print(f"Total: {self.total}");
    }
}

can 'Get Reciept Object'
get_reciept(reciept_img: 'Image of the Receipt': Image) -> 'Reciept Object': Receipt by llm();

with entry {
    reciept_img = Image.open("receipt.jpg");
    receipt = get_reciept(reciept_img);
    receipt.pp();
}